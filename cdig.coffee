#!/usr/bin/env coffee

child_process = require "child_process"
fs = require "fs"
keytar = require "keytar"
path = require "path"
promptSync = require "prompt-sync"


descriptions =
  Tool:
    help: "Display this helpful information"
    version: "Display the cdig tool's version number"
    update: "Install the latest version of the cdig tool"
    upgrade: "Same as update, but also grabs homebrew and npm dependencies"

  LBS:
    auth: "Get/set your LBS API token — don't forget to set up AWS too!"

  Project:
    new: "Set up a new project in this folder"
    pull: "Download system files needed to run the project"
    clean: "Delete system files and folders generated by other commands\n"

    watch: "Continually make & serve a local development build of the project\n"

    compile: "Make an optimized deployable build of the project"
    push: "Upload the deployable build to our content delivery network"
    register: "Create an Artifact on LBS for the uploaded build\n"

    run: "clean + pull + watch"
    deploy: "compile + push + register"



# Helpers #########################################################################################


# Who needs chalk when you can just roll your own ANSI escape sequences
do ()->
  global.white = (t)-> t
  for color, n of red: "31", green: "32", yellow: "33", blue: "34", magenta: "35", cyan: "36"
    do (color, n)-> global[color] = (t)-> "\x1b[#{n}m" + t + "\x1b[0m"

err = (msg)-> console.log red "\n  Error: " + msg

# Saner default for execSync
exec = (cmd)-> child_process.execSync cmd, stdio: "inherit"

# Little sugary filesystem helpers
exists = (filePath)-> fs.existsSync filePath
readdir = (filePath)-> fs.readdirSync filePath
mkdir = (filePath)-> fs.mkdirSync filePath, recursive: true
rm = (filePath)-> if exists filePath then fs.rmSync filePath, recursive: true
isDir = (filePath)-> fs.statSync(filePath).isDirectory()
read = (filePath)-> if exists filePath then fs.readFileSync(filePath).toString()

post = (url, data)->
  token = await keytar.getPassword "com.lunchboxsessions.cli", "api-token"
  await fetch url,
    method: "POST"
    headers:
      "Content-Type": "application/json"
      "X-LBS-API-TOKEN": token
    body: JSON.stringify data

download = (url, filePath)->
  exec "curl --create-dirs -fsSo #{filePath} #{url}"

isUrl = (str)->
  str.startsWith("http://") or str.startsWith("https://")

prompt = (question, answers)->
  console.log yellow question
  for k, v of answers
    console.log "Enter #{k} for #{v}"
  answer = promptSync(sigint:true) "Answer: "
  if answers[answer]
    answers[answer]
  else
    console.log red "... Wasn't expecting that answer."

# Get or prompt for the project type. This function is stateful to avoid redundant prompts.
projectType = null
getProjectType = ()->
  projectType = if projectType
    projectType
  else if t = read("cdig.json")?.type
    t
  else if exists "source/config.coffee"
    "svga"
  else if exists "source/index.kit"
    "cd-module"
  else
    prompt "What type of project is this?", {m: "cd-module", s: "svga"}

commandHasNeededFiles = ({command, files, hint})->
    hasFiles = exists files
    if not hasFiles then console.log red "Cannot #{command} — please run " + yellow "cdig #{hint} " + red "first"
    return hasFiles


# Commands ########################################################################################

commands = {}


# Tool Commands

commands.help = ()->
  console.log "\n  You can run any of the commands listed below. For example, run " + yellow("cdig help") + " to see this info."
  for label, group of descriptions
    console.log ""
    console.log green "  " + label + " Commands:"
    console.log ""
    for name, description of group
      console.log yellow "    " + name.padEnd(11) + blue description
  console.log ""

commands.version = ()->
  console.log require("./package.json").version

commands.update = ()->
  console.log yellow "Installing the latest version of the CDIG tool..."
  exec "npm i -g cdig"

commands.upgrade = ()->
  console.log yellow "Installing the latest version of the CDIG tool and all dependencies..."
  console.log green "brew upgrade"
  exec "brew upgrade"
  console.log green "brew cleanup"
  exec "brew cleanup"
  console.log green "npm i -g npm"
  exec "npm i -g npm"
  console.log green "npm i -g cdig"
  exec "npm i -g cdig"


# LBS Commands

# Get/set the LBS API token for this user
commands.auth = (token)->
  if token?
    keytar.setPassword "com.lunchboxsessions.cli", "api-token", token
    console.log green "Your API token has been saved"
  else
    token = await keytar.getPassword "com.lunchboxsessions.cli", "api-token"
    console.log yellow "Your current token is: " + blue token


# Project Commands

era = "v4-1" # TODO — make this dynamic
systemFiles = [".gitignore", "cdig.json", "package.json"]
generatedFiles = [".DS_Store", ".git", "deploy", "gulpfile.coffee", "node_modules", "package-lock.json", "public", "yarn.lock", "yarn-error.log"]
newProjectFiles =
  "cd-module": ["source/index.kit", "source/pages/objectives.html"]
  svga: ["source/root.coffee", "source/config.coffee"]

pullFromOrigin = (type, files)->
  baseUrl = "https://raw.githubusercontent.com/cdig/#{type}-starter/#{era}/dist/"
  for file in files
    download baseUrl + file, file

pullNodeModules = ()->
  if exists "~/cdig/cli/node_modules"
    exec "cp -a ~/cdig/cli/node_modules ."
  else
    exec "npm update --silent"

gulp = (cmd)->
  exec "gulp --gulpfile node_modules/cd-core/gulpfile.coffee --cwd . #{cmd}"

last = (arr)-> arr[arr.length-1]

projectName = ()->
  last process.cwd().split(path.sep)

indexName = ()->
  if isDir "deploy"
    path.basename fs.readdirSync("deploy/index")[0]

indexFragment = (fragmentName)->
  file = read "deploy/all/#{indexName()}"
  beginMarker = "<!--! begin " + fragmentName + " -->"
  endMarker = "<!--! end " + fragmentName + " -->"
  beginIndex = file.indexOf(beginMarker) + beginMarker.length
  endIndex = file.indexOf endMarker
  file[beginIndex...endIndex]

indexHead = ()-> indexFragment "head"
indexBody = ()-> indexFragment "body"


commands.new = ()->
  if exists "source"
    console.log red "This folder already contains a project"
  else
    if type = getProjectType()
      console.log yellow "Creating a new #{type} project..."
      typeFiles = newProjectFiles[type]
      mkdir "resources"
      pullFromOrigin type, typeFiles

commands.pull = ()->
  return unless commandHasNeededFiles command: "watch", files: "source", hint: "new"
  console.log yellow "Downloading system files and dependencies..."
  type = getProjectType()
  rm file for file in systemFiles
  pullFromOrigin type, systemFiles
  pullNodeModules() # TODO: split this out into its own command, so that I can run it separately when debugging dependency stuff

commands.clean = ()->
  console.log yellow "Cleaning..."
  rm file for file in systemFiles
  rm file for file in generatedFiles


commands.watch = ()->
  return unless commandHasNeededFiles command: "watch", files: "node_modules", hint: "pull"
  console.log yellow "Watching... (press control-c to stop)"
  gulp getProjectType() + ":dev"


commands.compile = ()->
  return unless commandHasNeededFiles command: "compile", files: "node_modules", hint: "pull"
  console.log yellow "Compiling deployable build..."
  gulp getProjectType() + ":prod"

commands.push = ()->
  return unless commandHasNeededFiles command: "push", files: "deploy", hint: "compile"
  console.log yellow "Pushing to S3..."
  exec "aws s3 sync deploy/all s3://lbs-cdn/#{era}/ --size-only --exclude \".*\" --cache-control max-age=31536000,immutable"

commands.register = ()->
  return unless commandHasNeededFiles command: "register", files: "deploy", hint: "compile"
  console.log yellow "Registering with LBS..."
  # TODO — would be nice to have a flag to do a deploy to localhost, for my testing
  domain = "https://www.lunchboxsessions.com"
  # domain = "http://localhost:3000"
  res = await post domain + "/cli/artifacts",
    era: era
    name: projectName()
    source: indexName()
    head: indexHead()
    body: indexBody()
  text = await res.text()
  console.log yellow text
  if isUrl text
    exec "open #{text}"
  else
    console.log red text


commands.run = ()->
    do commands[c] for c in ["clean", "pull", "watch"]

commands.deploy = ()->
  do commands[c] for c in ["compile", "push", "register"]



# Main ############################################################################################

args = process.argv[2..]

command = args.shift()
command ?= "help"
command = command.replace "--", ""

if c = commands[command]
  c ...args
else
  err yellow command + red " is not a valid command."
  commands.help()
